Constant Story "The Trampoline^";

                       Include "vorple.h";

Replace AfterGameOver;      ! to recognize new verbs at the final prompt

Include "parser";
                       
Object LibraryMessages
	with before [; Miscellany: if (lm_n == 5) { EndMessage(); rtrue; }
	];

Include "verblib";
                       Include "vorple-multimedia.h";
Include "grammar";

[ Initialise ;
	location = trampoline;
	VorpleInitialise();
];

Object trampoline "Trampoline"
	with description "You're on a huge trampoline. You know what to do!",
	before [; Jump: print "Whee!"; deadflag=2; rtrue; ],
has light;







! Copied and pasted from the Vorple library, the last few lines are the interesting ones
[ AfterGameOver i;

  .RRQPL;

    L__M(##Miscellany,5);

  .RRQL;

    print "> ";
    #Ifdef TARGET_ZCODE;
    #IfV3; read buffer parse; #Endif; ! V3
    temp_global=0;
    #IfV5; read buffer parse DrawStatusLine; #Endif; ! V5
    #Ifnot; ! TARGET_GLULX
    KeyboardPrimitive(buffer, parse);
    #Endif; ! TARGET_
    i = parse-->1;
    if (i == QUIT1__WD or QUIT2__WD) {
        #Ifdef TARGET_ZCODE;
        quit;
        #Ifnot; ! TARGET_GLULX
        quit;
        #Endif; ! TARGET_
    }
    if (i == RESTART__WD) {
        #Ifdef TARGET_ZCODE;
        if (isVorpleSupported()) { VorpleExecuteJavaScriptCommand("window.location.reload();"); rtrue; } else { @restart; }
        #Ifnot; ! TARGET_GLULX
        @restart;
        #Endif; ! TARGET_
    }
    if (i == RESTORE__WD) {
        RestoreSub();
        jump RRQPL;
    }
    if (i == FULLSCORE1__WD or FULLSCORE2__WD && TASKS_PROVIDED==0) {
        new_line; FullScoreSub();
        jump RRQPL;
    }
    if (deadflag == 2 && i == AMUSING__WD && AMUSING_PROVIDED==0) {
        new_line; Amusing();
        jump RRQPL;
    }
    #IfV5;
    if (i == UNDO1__WD or UNDO2__WD or UNDO3__WD) {
        if (undo_flag == 0) {
            L__M(##Miscellany, 6);
            jump RRQPL;
        }
        if (undo_flag == 1) jump UndoFailed2;
        #Ifdef TARGET_ZCODE;
        @restore_undo i;
        #Ifnot; ! TARGET_GLULX
        @restoreundo i;
        i = (~~i);
        #Endif; ! TARGET_
        if (i == 0) {
          .UndoFailed2;
            L__M(##Miscellany, 7);
        }
        jump RRQPL;
    }

    if (deadflag == 2 && i == 'watch') {
	if (IsVorpleSupported()) {
		VorplePlayYoutubeVideo("GifZWBxBDn8");
	} else {
		print "See your prize video at https://www.youtube.com/watch?v=GifZWBxBDn8.";
	}
	jump RRQPL;	
    }
    L__M(##Miscellany, 8);
    jump RRQL;
];


[ EndMessage ;
	print "^Would you like to RESTART, RESTORE a saved game";
	#Ifdef DEATH_MENTION_UNDO;
	print ", UNDO your last move";
	#Endif;
	if (TASKS_PROVIDED == 0) print ", give the FULL score for that game";
	if (deadflag == 2) { print ", WATCH your prize video"; }
	if (deadflag == 2 && AMUSING_PROVIDED == 0)
		print ", see some suggestions for AMUSING things to do";
        " or QUIT?";
];
